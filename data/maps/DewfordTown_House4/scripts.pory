raw`
DewfordTown_House4_MapScripts::
	map_script MAP_SCRIPT_ON_TRANSITION, DewfordTown_House4_OnTransition
	.byte 0

DewfordTown_House4_OnTransition:
	call_if_eq VAR_DEWFORD_TOWN_STATE, 1, DewfordTown_House4_EventScript_AddTropius
	end
`

script DewfordTown_House4_EventScript_AddTropius{
    setobjectxyperm(1, 8, 5)
    addobject(1)
    if(flag(FLAG_UNUSED_0x030) == FALSE){
        setobjectmovementtype(2, MOVEMENT_TYPE_WALK_IN_PLACE_RIGHT)
    }
    elif(flag(FLAG_UNUSED_0x030) == TRUE){
        setobjectmovementtype(2, MOVEMENT_TYPE_WANDER_AROUND)
    }
    end
}

script DewfordTown_House4_EventScript_LittleGirl{
    lockall
	faceplayer
    if(flag(FLAG_UNUSED_0x030) == FALSE && var(VAR_DEWFORD_TOWN_STATE) == 0){
        msgbox(format("It's scary here! I want to leave!"))
    }
    elif(flag(FLAG_UNUSED_0x030) == FALSE && var(VAR_DEWFORD_TOWN_STATE) == 1){
        msgbox(format("This Tropius just came into our house! He makes me so happy!"))
        checkitem(ITEM_AIR_BALLOON)
        if(var(VAR_RESULT) == TRUE){
            msgbox(format("Oh? Is that balloon for me? I love it!\pOk, I'll let the Tropius go."))
            closemessage
            setflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
            applymovement(2, Common_Movement_FaceRight)
            waitmovement(0)
            delay(16)
            applymovement(2, Common_Movement_WalkInPlaceRight)
            waitmovement(0)
            msgbox(format("Goodbye, Tropius!"))
            closemessage
            playmoncry(SPECIES_TROPIUS, CRY_MODE_NORMAL)
	        waitmoncry
            fadescreenswapbuffers(FADE_TO_BLACK)
	        removeobject(1)
            waitmovement(0)
            applymovement(2, Common_Movement_FacePlayer)
            setobjectmovementtype(2,MOVEMENT_TYPE_WANDER_AROUND)
            waitmovement(0)
	        playse(SE_EXIT)
	        fadescreenswapbuffers(FADE_FROM_BLACK)
            addvar(VAR_UNUSED_0x40F8, 1)
            setflag(FLAG_UNUSED_0x030)
	        fadedefaultbgm
        }
        elif(var(VAR_RESULT) == FALSE){
            msgbox(format("I won't ever let him go!"))
            closemessage
            waitmovement(0)
            applymovement(2, Common_Movement_FaceRight)
            waitmovement(0)
        }
    }
    elif(flag(FLAG_UNUSED_0x030) == TRUE){
        msgbox(format("Thank you for the balloon, but I do miss that Tropius..."))
    }
    releaseall
    end
}

script DewfordTown_House4_EventScript_Tropius{
    lockall
    faceplayer
    playmoncry(SPECIES_TROPIUS, CRY_MODE_NORMAL)
	waitmoncry
    msgbox(format("The Tropius roared with glee!"))
	fadedefaultbgm
    closemessage
    waitmovement(0)
    applymovement(1, Common_Movement_FaceLeft)
    waitmovement(0)
    releaseall
    end
}

script DewfordTown_House4_EventScript_OldMan{
    lockall
    faceplayer
    msgbox("This is no place to raise a child, but Stormy isn't able to evacuate people quickly enough.\pI don't know who to blame!")
    releaseall
    end
}